{
  parserClass="me.yiiguxing.demo.cls.htmlx.parser.SimpleHTMLParser"
  extends="me.yiiguxing.demo.cls.htmlx.psi.impl.SimpleHTMLElementBase"
  implements="me.yiiguxing.demo.cls.htmlx.psi.SimpleHTMLElement"

  psiClassPrefix="SimpleHTML"
  psiImplClassSuffix="Impl"
  psiPackage="me.yiiguxing.demo.cls.htmlx.psi"
  psiImplPackage="me.yiiguxing.demo.cls.htmlx.psi.impl"

  elementTypeHolderClass="me.yiiguxing.demo.cls.htmlx.psi.SimpleHTMLTypes"
  elementTypeClass="me.yiiguxing.demo.cls.htmlx.psi.SimpleHTMLElementType"
  tokenTypeClass="me.yiiguxing.demo.cls.htmlx.psi.SimpleHTMLTokenType"

  psiImplUtilClass="me.yiiguxing.demo.cls.htmlx.psi.impl.SimpleHTMLPsiImplUtil"

  tokens=[
    TAG_START='<'
    TAG_END='>'
    END_TAG_START='</'
    EMPTY_TAG_END='/>'
    EQ='='
    COMMENT_START='<!--'
    COMMENT_END='-->'
    ATTRIBUTE_VALUE_DEFINER_SQ="'"
    ATTRIBUTE_VALUE_DEFINER_DQ='"'
    DOCTYPE='<!DOCTYPE html>'
  ]

  extends(".*Tag")=Tag
}

SimpleHTMLFile ::= DOCTYPE? (Tag|Comment)*

Tag ::= NotEmptyTag|EmptyTag {
  methods=[getTagNameElement getAttributeList]
}
NotEmptyTag ::= TagStart (Tag|Comment|Text)* TagEnd {
  pin=1
  methods=[getStartTagNameElement getEndTagNameElement]
}
EmptyTag ::= TagHead '/>'
private TagStart ::= TagHead '>'
private TagHead ::= '<' START_TAG_NAME Attribute* {pin=1}
private TagEnd ::= '</' END_TAG_NAME '>' {pin=1}
Attribute ::= ATTRIBUTE_NAME ('=' AttributeValue)? {pin=1}
AttributeValue ::= '"' ATTR_VALUE? '"' | "'" ATTR_VALUE? "'"
Text ::= ROW_TEXT+

Comment ::= '<!--' COMMENT_TEXT? '-->'  {
    pin=1
    implements=["me.yiiguxing.demo.cls.htmlx.psi.SimpleHTMLElement" "com.intellij.psi.PsiComment"]
    extends="me.yiiguxing.demo.cls.htmlx.psi.impl.SimpleHTMLCommentBase"
}
